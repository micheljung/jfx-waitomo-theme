import org.gradle.internal.os.OperatingSystem

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

group = "io.github.micheljung"
version "${version}"
description = "Modern dark theme for JavaFX applications"

project.ext.javaFxPlatform = "${javaFxPlatform}"

allprojects {
  version "${version}"
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11

  repositories {
    mavenCentral()
  }
}

java {
  withSourcesJar()
  withJavadocJar()
}

if (javaFxPlatform == "unspecified") {
  switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
      project.ext.javaFxPlatform = "linux"
      break
    case OperatingSystem.MAC_OS:
      project.ext.javaFxPlatform = "mac"
      break
    case OperatingSystem.WINDOWS:
      project.ext.javaFxPlatform = "win"
      break
  }
}

dependencies {
  compileOnly "org.openjfx:javafx-base:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-controls:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-fxml:${javaFxVersion}:${javaFxPlatform}"
  compileOnly "org.openjfx:javafx-graphics:${javaFxVersion}:${javaFxPlatform}"
}

publishing {
  repositories {
    maven {
      name = "OSSRH"
      setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
      credentials {
        username = System.getenv("OSSRH_USERNAME")
        password = System.getenv("OSSRH_TOKEN")
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      groupId = "io.github.micheljung"
      artifactId 'jfx-waitomo-theme'

      from components.java

      pom {
        name = 'JFX Waitomo Theme'
        description = 'Modern dark theme for JavaFX'
        url = 'https://github.com/micheljung/jfx-waitomo-theme'
        licenses {
          license {
            name = 'GNU General Public License, Version 3.0'
            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
          }
        }
        developers {
          developer {
            id = 'mj'
            name = 'Michel Jung'
            email = 'michel.jung89@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/micheljung/jfx-waitomo-theme.git'
          developerConnection = 'scm:git:git@github.com:micheljung/jfx-waitomo-theme.git'
          url = 'https://github.com/micheljung/jfx-waitomo-theme'
        }
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}
